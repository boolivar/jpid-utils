plugins {
	id 'java-library'
	id 'maven'
	id 'net.nemerosa.versioning' version '2.6.1'
}

versioning {
	branchEnv = ['TRAVIS_BRANCH']
}

group = 'org.bool.jpid'
version = versioning.info.display

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	implementation 'net.java.dev.jna:jna-platform:4.5.1'
	testImplementation 'junit:junit:4.12'
}

jar {
	manifest {
		attributes('Implementation-Title': project.name,
				'Implementation-Version': project.version)
	}
}

task writePom {
	doLast {
		pom {
			writeTo("${buildDir}/libs/${project.name}-${version}.pom")
		}
	}
}

task sourceJar(type: Jar) {
	group 'Build'
	description 'Package source in jar'
	from sourceSets.main.allSource
	classifier 'sources'
}

artifacts {
	archives sourceJar
}

task travisDeploy {
	doLast {
		def now = java.time.LocalDate.now()
		file("${buildDir}/travis-deploy.json").text = """
		{
			"package": {
				"name": "${project.name}",
				"repo": "java",
				"subject": "boolivar",
				"desc": "PID utils for Java",
				"issue_tracker_url": "https://github.com/boolivar/jpid-utils/issues",
				"vcs_url": "https://github.com/boolivar/jpid-utils",
				"licenses": ["MIT"],
				"labels": ["PID"],
				"public_download_numbers": true,
				"public_stats": true
			},

			"version": {
				"name": "${version}",
				"released": "${now}",
				"vcs_tag": "${version}"
			},

			"files":
				[{"includePattern": "${buildDir}/libs/(.*)", "uploadPattern": "/org/bool/jpid/jpid-utils/${version}/\$1"}],
			"publish": true
		}"""
	}
	dependsOn([buildArchives, writePom])
}